---
layout: single
title: 2024/06/04/ SQL -14 - PL SQL
---
--PL/SQL (PROCEDURAL LANGUAGE EXTENSION SQL): A procedural programming language for writing procedural programs to provide variable declaration, conditional processing, and loop processing that cannot be implemented with SQL commands alone.
--PROCEDURE: A program created using PL/SQL

--PL/SQL procedures are written by dividing into three sections:
--1. DECLARE section (declaration): Starts the section using the DECLARE keyword - the section where commands for variable declaration are written (optional)
--2. EXECUTABLE section (execution): Starts the section using the BEGIN keyword - the section where executable commands including SQL commands are written (mandatory)
--3. EXCEPTION section (exception handling): Starts the section using the EXCEPTION keyword - the section where commands for exception handling are written (optional)

--Commands written within the sections are separated by the ; symbol.
--PL/SQL procedures end using the END keyword.
--To execute a PL/SQL procedure, use the / symbol at the end of the procedure.

--Change the session's environment variable settings to output messages to the server console
```
SET SERVEROUTPUT ON;
```

--Function to output messages to the server console in a PL/SQL procedure
--Syntax: DBMS_OUTPUT.PUT_LINE(output_message)

--Write a PL/SQL procedure to output a welcome message to the server console
```
BEGIN
    DBMS_OUTPUT.PUT_LINE('HELLO, ORACLE!!!');
END;
/
```
--Variable declaration and storing initial values - Declaration section
--Syntax: variable_name [CONSTANT] datatype [NOT NULL] [{:= expression|DEFAULT}]
--CONSTANT: A keyword used to set the variable value to be unchangeable - used to create constants
--NOT NULL: A keyword to ensure that the variable must have a value - NULL not allowed
--To store a value in a variable, use the assignment operator (:= operator)
--Expression: A way to represent a value - numeric value, character value, date value, or variable (stored value), arithmetic expression (result), function (return value)

--Change the initial value stored in the variable - Execution section
--Syntax: variable_name := expression

--Scalar variable: A variable declared using Oracle data types

--Write a PL/SQL procedure to declare scalar variables, store values, and output the values stored in the variables to the server console
```
DECLARE
    VEMPNO NUMBER(4) := 7788;
    VENAME VARCHAR2(20) := 'SCOTT';
BEGIN
    DBMS_OUTPUT.PUT_LINE('Employee Number / Employee Name');
    DBMS_OUTPUT.PUT_LINE('-----------------');
    DBMS_OUTPUT.PUT_LINE(VEMPNO||' / '||VENAME);
    DBMS_OUTPUT.PUT_LINE('-----------------');
    
    VEMPNO := 7893;
    VENAME := 'KING';
    DBMS_OUTPUT.PUT_LINE(VEMPNO||' / '||VENAME);
    DBMS_OUTPUT.PUT_LINE('-----------------');
END;
/
```

SET SERVEROUT ON;

--Reference variable: A variable that references the data type of another variable or a table column - Declaration section
--Format) Variable_Name {Variable_Name%TYPE|Table_Name.Column_Name%TYPE}

--Command to search for rows stored in a table and store column values into variables - Execution section
--Format) SELECT Target_Column1, Target_Column2, ... INTO Variable1, Variable2, ... FROM Table_Name [WHERE Condition]
--The number and data types of the targets and variables must match exactly.

--Create a PL/SQL procedure that retrieves the employee number and name from the EMP table where the employee name is SCOTT, 
--stores them in reference variables created by referencing the data types of the EMPNO column and ENAME column, 
--and outputs the values to the server console.
DECLARE
    VEMPNO EMP.EMPNO%TYPE;
    VENAME EMP.ENAME%TYPE;
BEGIN
    SELECT EMPNO,ENAME INTO VEMPNO,VENAME FROM EMP WHERE ENAME='SCOTT';
    DBMS_OUTPUT.PUT_LINE('Employee Number / Employee Name');
    DBMS_OUTPUT.PUT_LINE('-----------------');
    DBMS_OUTPUT.PUT_LINE(VEMPNO||' / '||VENAME);
    DBMS_OUTPUT.PUT_LINE('-----------------');
END;
/

--Table variable: A variable used to store multiple values - similar to arrays: used to store the values of one column of multiple rows
--Format) Table_Variable_Name Table_Type_Name   
--The table type, which is the data type of the table variable, must be declared first to create a table variable using the table type.
--Format) TYPE Table_Type_Name IS TABLE OF {Data_Type|Variable_Name%TYPE|Table_Name.Column_Name%TYPE} [NOT NULL] [INDEX BY BINARY_INTEGER]

--Table variables store values by distinguishing them using elements, and elements can be distinguished using subscripts (numbers increasing from 1).
--Format) Table_Variable_Name(Index)

--Create table variables by referencing the data types of the EMPNO column and ENAME column of the EMP table, 
--retrieve the employee numbers and names of all employees stored in the EMP table, 
--store them in table variables, and output the element values of the table variable to the server console.
DECLARE
    /* Type declaration */
    TYPE VEMPNO_TABLE_TYPE IS TABLE OF EMP.EMPNO%TYPE INDEX BY BINARY_INTEGER;
    TYPE VENAME_TABLE_TYPE IS TABLE OF EMP.ENAME%TYPE INDEX BY BINARY_INTEGER;

    /* Create table variables using the table type */
    VEMPNO_TABLE VEMPNO_TABLE_TYPE;
    VENAME_TABLE VENAME_TABLE_TYPE;
       
    /* Scalar variable declaration for iterating through table variable elements */
    I BINARY_INTEGER := 0;
BEGIN
    /* Loop to store the employee numbers and names retrieved from the EMP table in table variables */
    /* FOR LOOP to store retrieved rows from SELECT command in an internal cursor and 
    store the retrieved rows into record variables one by one */
    FOR K IN (SELECT EMPNO,ENAME FROM EMP) LOOP
        I := I + 1;
        VEMPNO_TABLE(I) := K.EMPNO;
        VENAME_TABLE(I) := K.ENAME;
    END LOOP;    

    DBMS_OUTPUT.PUT_LINE('Employee Number / Employee Name');
    DBMS_OUTPUT.PUT_LINE('-----------------');
    /* Loop to output the values stored in the table variables to the server console */
    FOR J IN 1..I LOOP
           DBMS_OUTPUT.PUT_LINE(VEMPNO_TABLE(J)||' / '||VENAME_TABLE(J));
    END LOOP;
END;
/

--Record variable: A variable used to store multiple values using fields - similar to objects: used to store multiple column values of a single row
--Format) Record_Variable_Name Record_Type_Name
--The record type, which is the data type of the record variable, must be declared first to create a record variable using the record type.
--Format) TYPE Record_Type_Name IS RECORD (Field_Name {Data_Type|Variable_Name%TYPE|Table_Name.Column_Name%TYPE}
--      , Field_Name {Data_Type|Variable_Name%TYPE|Table_Name.Column_Name%TYPE}, ...)

--Record variables store values using fields, and record variable fields can be accessed using the [.] symbol.
--Format) Record_Variable_Name.Field_Name

--Create record variables by referencing the EMPNO, NAME, JOB, SAL, DEPTNO columns of the EMP table, 
--retrieve the employee number, name, job, salary, and department number of the employee with the employee number 7844 from the EMP table, 
--store them in a record variable, and output the field values of the record variable to the server console.
DECLARE  
    /* Type declaration */
    TYPE VEMP_RECORD_TYPE IS RECORD(VEMPNO EMP.EMPNO%TYPE,VENAME EMP.ENAME%TYPE
        ,VJOB EMP.JOB%TYPE,VSAL EMP.SAL%TYPE,VDEPTNO EMP.DEPTNO%TYPE);

    /* Create record variable using record type */
    VEMP_RECORD VEMP_RECORD_TYPE;
BEGIN
    /* Assign the column values of the selected row to the fields of the record variable */
    SELECT EMPNO,ENAME,JOB,SAL,DEPTNO INTO VEMP_RECORD.VEMPNO,VEMP_RECORD.VENAME
        ,VEMP_RECORD.VJOB,VEMP_RECORD.VSAL,VEMP_RECORD.VDEPTNO
    FROM EMP WHERE EMPNO=7844;

   /* Output the field values of the record variable to the server console */
    DBMS_OUTPUT.PUT_LINE('Employee Number = '||VEMP_RECORD.VEMPNO);
    DBMS_OUTPUT.PUT_LINE('Employee Name = '||VEMP_RECORD.VENAME);
    DBMS_OUTPUT.PUT_LINE('Employee Job = '||VEMP_RECORD.VJOB);
    DBMS_OUTPUT.PUT_LINE('Employee Salary = '||VEMP_RECORD.VSAL);
    DBMS_OUTPUT.PUT_LINE('Department Number = '||VEMP_RECORD.VDEPTNO);
END;
/

--Record variables can be created without a record type by referencing rows of a table
--Format) Record variable name %ROWTYPE of table name
--A record variable created based on the columns of a table automatically generates fields with the same name and data type

--Create a record variable by referencing the columns EMPNO, NAME, JOB, SAL, and DEPTNO of the EMP table, 
--search for the employee with the employee number 7844, store the employee number, employee name, job, salary, and department number in the record variable, 
--and output the field values of the record variable to the server console in a PL/SQL procedure
DECLARE
    /* Declare a record variable by referencing rows of a table - the field names of the record variable are the same as the column names of the table */
    VEMP_RECORD EMP%ROWTYPE;
BEGIN
    /* Automatically save all column values of the retrieved single row to the fields of the record variable */
    SELECT * INTO VEMP_RECORD FROM EMP WHERE EMPNO=7844;

    /* Output the field values of the record variable to the server console */
    DBMS_OUTPUT.PUT_LINE('Employee Number = '||VEMP_RECORD.EMPNO);
    DBMS_OUTPUT.PUT_LINE('Employee Name = '||VEMP_RECORD.ENAME);
    DBMS_OUTPUT.PUT_LINE('Employee Job = '||VEMP_RECORD.JOB);
    DBMS_OUTPUT.PUT_LINE('Employee Salary = '||VEMP_RECORD.SAL);
    DBMS_OUTPUT.PUT_LINE('Department Number = '||VEMP_RECORD.DEPTNO);
END;
/

--Selection statement: a statement for selecting and executing commands based on conditions
--IF: a selection statement that executes commands based on the result of a condition
--Format) IF (condition) THEN command; command; ... END IF;
--Parentheses for writing conditions () can be omitted

--Search for information of the employee with employee number 7788 in the EMP table, 
--and output the employee number, employee name, and department name to the server console in a PL/SQL procedure
--Output the department name using the department number - 10: ACCOUNTING, 20: RESEARCH, 30: SALES, 40: OPERATION
DECLARE
    VEMP EMP%ROWTYPE; /* Declare a record variable to store one row (employee information) of the EMP table */
    VDNAME VARCHAR2(20) := NULL; /* Declare a scalar variable to store the department name */
BEGIN
    SELECT * INTO VEMP FROM EMP WHERE EMPNO=7788;
    
    /* Store the department name in a variable by comparing the field value storing the department number of the record variable */
    IF(VEMP.DEPTNO = 10) THEN VDNAME := 'ACCOUNTING';
    ELSIF(VEMP.DEPTNO = 20) THEN VDNAME := 'RESEARCH';
    ELSIF(VEMP.DEPTNO = 30) THEN VDNAME := 'SALES';
    ELSIF(VEMP.DEPTNO = 40) THEN VDNAME := 'OPERATION'; END IF;

    DBMS_OUTPUT.PUT_LINE('Employee Number = '||VEMP.EMPNO);
    DBMS_OUTPUT.PUT_LINE('Employee Name = '||VEMP.ENAME);
    DBMS_OUTPUT.PUT_LINE('Department Name = '||VDNAME);   
END;
/

--Format) IF (condition) THEN command; command; ... ELSE command; command; ... END IF;

--Search for information of the employee with employee number 7788 in the EMP table, 
--and output the employee number, employee name, and annual salary to the server console in a PL/SQL procedure
--Annual salary: If there is a commission - (salary + commission) * 12, If there is no commission - salary * 12
DECLARE
    VEMP EMP%ROWTYPE; /* Declare a record variable to store one row (employee information) of the EMP table */
    ANNUAL NUMBER(7,2) := 0; /* Declare a scalar variable to store the annual salary */    
BEGIN
    SELECT * INTO VEMP FROM EMP WHERE EMPNO=7788;
    
    IF VEMP.COMM IS NOT NULL THEN
        ANNUAL := ( VEMP.SAL + VEMP.COMM ) * 12;
    ELSE 
        ANNUAL := VEMP.SAL * 12;
    END IF;     
 
    DBMS_OUTPUT.PUT_LINE('Employee Number = '||VEMP.EMPNO);
    DBMS_OUTPUT.PUT_LINE('Employee Name = '||VEMP.ENAME);
    DBMS_OUTPUT.PUT_LINE('Employee Annual Salary = '||ANNUAL);     
END;
/

--Format) IF (condition) THEN command; command; ... ELSIF (condition) command; command; ... ELSE command; command; ... END IF;

--Search for information of the employee with employee number 7788 in the EMP table, 
--and output the employee number, employee name, and department name to the server console in a PL/SQL procedure
--Output the department name using the department number - 10: ACCOUNTING, 20: RESEARCH, 30: SALES, 40: OPERATION
--Also, create a PL/SQL procedure to output the names of all departments
DECLARE
    VEMP EMP%ROWTYPE; /* Declare a record variable to store one row (employee information) of the EMP table */
    VDNAME VARCHAR2(20) := NULL; /* Declare a scalar variable to store the department name */
    CURSOR VDEPT IS SELECT * FROM DEPT; /* Create a cursor to retrieve information of all departments */
BEGIN
    SELECT * INTO VEMP FROM EMP WHERE EMPNO=7788;
    
    IF(VEMP.DEPTNO = 10) THEN VDNAME := 'ACCOUNTING';
    ELSIF(VEMP.DEPTNO = 20) THEN VDNAME := 'RESEARCH';
    ELSIF(VEMP.DEPTNO = 30) THEN VDNAME := 'SALES';
    ELSIF(VEMP.DEPTNO = 40) THEN VDNAME := 'OPERATION'; END IF;

    DBMS_OUTPUT.PUT_LINE('Employee Number = '||VEMP.EMPNO);
    DBMS_OUTPUT.PUT_LINE('Employee Name = '||VEMP.ENAME);
    DBMS_OUTPUT.PUT_LINE('Department Name = '||VDNAME);   
--CASE: A selection statement that chooses and executes commands based on the value stored in a variable or using conditional expressions.
--Format) CASE variable WHEN comparison_value1 THEN command; command; ... WHEN comparison_value2 THEN command; command; ... END CASE
--Commands are chosen and executed based on the value stored in the variable compared with comparison values.

--Write a PL/SQL procedure to search for employee information with the employee number 7788 in the EMP table,
--and output the employee number, employee name, job, salary, and actual salary by job to the server console.
--Actual salary by job: The actual salary to be paid to employees, categorized by job.
--ANALYST: salary * 1.1, CLERK: salary * 1.2, MANAGER: salary * 1.3, PRESIDENT: salary * 1.4, SALESMAN: salary * 1.5
DECLARE
    VEMP EMP%ROWTYPE;
    VPAY NUMBER(7,2); -- Scalar variable to store the actual salary by job
BEGIN
    SELECT * INTO VEMP FROM EMP WHERE EMPNO=7788;
    
    CASE VEMP.JOB 
        WHEN 'ANALYST' THEN VPAY := VEMP.SAL  * 1.1;
        WHEN 'CLERK' THEN VPAY := VEMP.SAL  * 1.2;
        WHEN 'MANAGER' THEN VPAY := VEMP.SAL  * 1.3;
        WHEN 'PRESIDENT' THEN VPAY := VEMP.SAL  * 1.4;
        WHEN 'SALESMAN' THEN VPAY := VEMP.SAL  * 1.5;
    END CASE;
    
    DBMS_OUTPUT.PUT_LINE('Employee Number = '||VEMP.EMPNO);
    DBMS_OUTPUT.PUT_LINE('Employee Name = '||VEMP.ENAME);
    DBMS_OUTPUT.PUT_LINE('Employee Job = '||VEMP.JOB);
    DBMS_OUTPUT.PUT_LINE('Employee Salary = '||VEMP.SAL);        
    DBMS_OUTPUT.PUT_LINE('Actual Salary by Job = '||VPAY);      
END;
/

--Format) CASE WHEN condition1 THEN command; command; ... WHEN condition2 THEN command; command; ... END CASE
--Commands are chosen and executed based on the truth value of the conditions.

--Write a PL/SQL procedure to search for employee information with the employee number 7788 in the EMP table,
--and output the employee number, employee name, job, salary, and salary grade to the server console.
--Salary Grade - A: 4001~5000, B: 3001~4000, C: 2001~3000, D: 1001~2000, E: 1~1000 
DECLARE
    VEMP EMP%ROWTYPE;
    VGRADE VARCHAR2(1); -- Scalar variable to store the salary grade
BEGIN
    SELECT * INTO VEMP FROM EMP WHERE EMPNO=7788;
    
    CASE 
        WHEN VEMP.SAL BETWEEN 4001 AND 5000 THEN VGRADE := 'A';
        WHEN VEMP.SAL BETWEEN 3001 AND 4000 THEN VGRADE := 'B';
        WHEN VEMP.SAL BETWEEN 2001 AND 3000 THEN VGRADE := 'C';
        WHEN VEMP.SAL BETWEEN 1001 AND 2000 THEN VGRADE := 'D';
        WHEN VEMP.SAL BETWEEN 1 AND 1000 THEN VGRADE := 'E';
    END CASE;
    
    DBMS_OUTPUT.PUT_LINE('Employee Number = '||VEMP.EMPNO);
    DBMS_OUTPUT.PUT_LINE('Employee Name = '||VEMP.ENAME);
    DBMS_OUTPUT.PUT_LINE('Employee Job = '||VEMP.JOB);
    DBMS_OUTPUT.PUT_LINE('Employee Salary = '||VEMP.SAL);        
    DBMS_OUTPUT.PUT_LINE('Salary Grade = '||VGRADE);      
END;
/

--LOOP: A statement to execute commands repeatedly.

--BASIC LOOP: Infinite loop - Use the EXIT command with a selection statement to exit the loop based on a condition.
--Format) LOOP command; command; ... END LOOP;

--Write a PL/SQL procedure to print numbers from 1 to 5 on the server console.
DECLARE
    I NUMBER(1) := 1;
BEGIN
    LOOP
        DBMS_OUTPUT.PUT_LINE(I);
        I := I + 1;
        IF I > 5 THEN EXIT; END IF;
    END LOOP;
END;
/

--FOR LOOP: Used when the number of iterations is known.
--Format) FOR INDEX_COUNTER IN [REVERSE] LOWER_BOUND..HIGH_BOUND LOOP command; command; ... END LOOP;

--Write a PL/SQL procedure to calculate the sum of numbers from 1 to 10 and print it on the server console.
DECLARE
    TOT NUMBER(2) := 0;
BEGIN
    FOR I IN 1..10 LOOP
        TOT := TOT + I;
    END LOOP;
    DBMS_OUTPUT.PUT_LINE('Sum of numbers from 1 to 10 = '||TOT);
END;
/

--FOR LOOP can be used to process multiple rows retrieved from a table.
--Format) FOR RECORD_VARIABLE IN (SELECT column1, column2, ... FROM table_name [WHERE condition]) LOOP command; command; ... END LOOP;

