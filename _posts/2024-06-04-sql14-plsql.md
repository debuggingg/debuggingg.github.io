---
layout: single
title: 2024/06/04/ SQL -14 - PL SQL
---
---
# PL/Sql
---

- PL/SQL (PROCEDURAL LANGUAGE EXTENSION SQL): A procedural programming language for writing procedural programs to provide variable declaration, conditional processing, and loop processing that cannot be implemented with SQL commands alone.
- PROCEDURE: A program created using PL/SQL

- PL/SQL procedures are written by dividing into three sections:
- 1. DECLARE section (declaration): Starts the section using the DECLARE keyword - the section where commands for variable declaration are written (optional)
- 2. EXECUTABLE section (execution): Starts the section using the BEGIN keyword - the section where executable commands including SQL commands are written (mandatory)
- 3. EXCEPTION section (exception handling): Starts the section using the EXCEPTION keyword - the section where commands for exception handling are written (optional)

- Commands written within the sections are separated by the ; symbol.
- PL/SQL procedures end using the END keyword.
- To execute a PL/SQL procedure, use the / symbol at the end of the procedure.

- Change the session's environment variable settings to output messages to the server console
```sql
SET SERVEROUTPUT ON;
```

- Function to output messages to the server console in a PL/SQL procedure
- Syntax: DBMS_OUTPUT.PUT_LINE(output_message)

- Write a PL/SQL procedure to output a welcome message to the server console
```sql
BEGIN
    DBMS_OUTPUT.PUT_LINE('HELLO, ORACLE!!!');
END;
/
```
- Variable declaration and storing initial values - Declaration section
- Syntax: variable_name [CONSTANT] datatype [NOT NULL] [{:= expression|DEFAULT}]
- CONSTANT: A keyword used to set the variable value to be unchangeable - used to create constants
- NOT NULL: A keyword to ensure that the variable must have a value - NULL not allowed
- To store a value in a variable, use the assignment operator (:= operator)
- Expression: A way to represent a value - numeric value, character value, date value, or variable (stored value), arithmetic expression (result), function (return value)

- Change the initial value stored in the variable - Execution section
- Syntax: variable_name := expression

- Scalar variable: A variable declared using Oracle data types

- Write a PL/SQL procedure to declare scalar variables, store values, and output the values stored in the variables to the server console
```sql
DECLARE
    VEMPNO NUMBER(4) := 7788;
    VENAME VARCHAR2(20) := 'SCOTT';
BEGIN
    DBMS_OUTPUT.PUT_LINE('Employee Number / Employee Name');
    DBMS_OUTPUT.PUT_LINE('--- --- --- --- -');
    DBMS_OUTPUT.PUT_LINE(VEMPNO||' / '||VENAME);
    DBMS_OUTPUT.PUT_LINE('--- --- --- --- -');
    
    VEMPNO := 7893;
    VENAME := 'KING';
    DBMS_OUTPUT.PUT_LINE(VEMPNO||' / '||VENAME);
    DBMS_OUTPUT.PUT_LINE('--- --- --- --- -');
END;
/
```
---
---
## ReferenceVariable
---

SET SERVEROUT ON;

- Reference variable: A variable that references the data type of another variable or a table column - Declaration section
- Format) Variable_Name {Variable_Name%TYPE|Table_Name.Column_Name%TYPE}

- Command to search for rows stored in a table and store column values into variables - Execution section
- Format) SELECT Target_Column1, Target_Column2, ... INTO Variable1, Variable2, ... FROM Table_Name [WHERE Condition]
- The number and data types of the targets and variables must match exactly.

- Create a PL/SQL procedure that retrieves the employee number and name from the EMP table where the employee name is SCOTT, 
- stores them in reference variables created by referencing the data types of the EMPNO column and ENAME column, 
- and outputs the values to the server console.
```sql
DECLARE
    VEMPNO EMP.EMPNO%TYPE;
    VENAME EMP.ENAME%TYPE;
BEGIN
    SELECT EMPNO,ENAME INTO VEMPNO,VENAME FROM EMP WHERE ENAME='SCOTT';
    DBMS_OUTPUT.PUT_LINE('Employee Number / Employee Name');
    DBMS_OUTPUT.PUT_LINE('--- --- --- --- -');
    DBMS_OUTPUT.PUT_LINE(VEMPNO||' / '||VENAME);
    DBMS_OUTPUT.PUT_LINE('--- --- --- --- -');
END;
/
```

- Table variable: A variable used to store multiple values - similar to arrays: used to store the values of one column of multiple rows
- Format) Table_Variable_Name Table_Type_Name   
- The table type, which is the data type of the table variable, must be declared first to create a table variable using the table type.
- Format) TYPE Table_Type_Name IS TABLE OF {Data_Type|Variable_Name%TYPE|Table_Name.Column_Name%TYPE} [NOT NULL] [INDEX BY BINARY_INTEGER]

- Table variables store values by distinguishing them using elements, and elements can be distinguished using subscripts (numbers increasing from 1).
- Format) Table_Variable_Name(Index)

- Create table variables by referencing the data types of the EMPNO column and ENAME column of the EMP table, 
- retrieve the employee numbers and names of all employees stored in the EMP table, 
- store them in table variables, and output the element values of the table variable to the server console.
```sql
DECLARE
    /* Type declaration */
    TYPE VEMPNO_TABLE_TYPE IS TABLE OF EMP.EMPNO%TYPE INDEX BY BINARY_INTEGER;
    TYPE VENAME_TABLE_TYPE IS TABLE OF EMP.ENAME%TYPE INDEX BY BINARY_INTEGER;

    /* Create table variables using the table type */
    VEMPNO_TABLE VEMPNO_TABLE_TYPE;
    VENAME_TABLE VENAME_TABLE_TYPE;
       
    /* Scalar variable declaration for iterating through table variable elements */
    I BINARY_INTEGER := 0;
BEGIN
    /* Loop to store the employee numbers and names retrieved from the EMP table in table variables */
    /* FOR LOOP to store retrieved rows from SELECT command in an internal cursor and 
    store the retrieved rows into record variables one by one */
    FOR K IN (SELECT EMPNO,ENAME FROM EMP) LOOP
        I := I + 1;
        VEMPNO_TABLE(I) := K.EMPNO;
        VENAME_TABLE(I) := K.ENAME;
    END LOOP;    

    DBMS_OUTPUT.PUT_LINE('Employee Number / Employee Name');
    DBMS_OUTPUT.PUT_LINE('--- --- --- --- -');
    /* Loop to output the values stored in the table variables to the server console */
    FOR J IN 1..I LOOP
           DBMS_OUTPUT.PUT_LINE(VEMPNO_TABLE(J)||' / '||VENAME_TABLE(J));
    END LOOP;
END;
/
```
---
## RecordVariable 
---
- Record variable: A variable used to store multiple values using fields - similar to objects: used to store multiple column values of a single row
- Format) Record_Variable_Name Record_Type_Name
- The record type, which is the data type of the record variable, must be declared first to create a record variable using the record type.
- Format) TYPE Record_Type_Name IS RECORD (Field_Name {Data_Type|Variable_Name%TYPE|Table_Name.Column_Name%TYPE}
-       , Field_Name {Data_Type|Variable_Name%TYPE|Table_Name.Column_Name%TYPE}, ...)

- Record variables store values using fields, and record variable fields can be accessed using the [.] symbol.
- Format) Record_Variable_Name.Field_Name

- Create record variables by referencing the EMPNO, NAME, JOB, SAL, DEPTNO columns of the EMP table, 
- retrieve the employee number, name, job, salary, and department number of the employee with the employee number 7844 from the EMP table, 
- store them in a record variable, and output the field values of the record variable to the server console.
```sql
DECLARE  
    /* Type declaration */
    TYPE VEMP_RECORD_TYPE IS RECORD(VEMPNO EMP.EMPNO%TYPE,VENAME EMP.ENAME%TYPE
        ,VJOB EMP.JOB%TYPE,VSAL EMP.SAL%TYPE,VDEPTNO EMP.DEPTNO%TYPE);

    /* Create record variable using record type */
    VEMP_RECORD VEMP_RECORD_TYPE;
BEGIN
    /* Assign the column values of the selected row to the fields of the record variable */
    SELECT EMPNO,ENAME,JOB,SAL,DEPTNO INTO VEMP_RECORD.VEMPNO,VEMP_RECORD.VENAME
        ,VEMP_RECORD.VJOB,VEMP_RECORD.VSAL,VEMP_RECORD.VDEPTNO
    FROM EMP WHERE EMPNO=7844;

   /* Output the field values of the record variable to the server console */
    DBMS_OUTPUT.PUT_LINE('Employee Number = '||VEMP_RECORD.VEMPNO);
    DBMS_OUTPUT.PUT_LINE('Employee Name = '||VEMP_RECORD.VENAME);
    DBMS_OUTPUT.PUT_LINE('Employee Job = '||VEMP_RECORD.VJOB);
    DBMS_OUTPUT.PUT_LINE('Employee Salary = '||VEMP_RECORD.VSAL);
    DBMS_OUTPUT.PUT_LINE('Department Number = '||VEMP_RECORD.VDEPTNO);
END;
/
```

- Record variables can be created without a record type by referencing rows of a table
- Format) Record variable name %ROWTYPE of table name
- A record variable created based on the columns of a table automatically generates fields with the same name and data type

- Create a record variable by referencing the columns EMPNO, NAME, JOB, SAL, and DEPTNO of the EMP table, 
- search for the employee with the employee number 7844, store the employee number, employee name, job, salary, and department number in the record variable, 
- and output the field values of the record variable to the server console in a PL/SQL procedure
```sql
DECLARE
    /* Declare a record variable by referencing rows of a table - the field names of the record variable are the same as the column names of the table */
    VEMP_RECORD EMP%ROWTYPE;
BEGIN
    /* Automatically save all column values of the retrieved single row to the fields of the record variable */
    SELECT * INTO VEMP_RECORD FROM EMP WHERE EMPNO=7844;

    /* Output the field values of the record variable to the server console */
    DBMS_OUTPUT.PUT_LINE('Employee Number = '||VEMP_RECORD.EMPNO);
    DBMS_OUTPUT.PUT_LINE('Employee Name = '||VEMP_RECORD.ENAME);
    DBMS_OUTPUT.PUT_LINE('Employee Job = '||VEMP_RECORD.JOB);
    DBMS_OUTPUT.PUT_LINE('Employee Salary = '||VEMP_RECORD.SAL);
    DBMS_OUTPUT.PUT_LINE('Department Number = '||VEMP_RECORD.DEPTNO);
END;
/
```


---
#### If

- Selection statement: a statement for selecting and executing commands based on conditions
- IF: a selection statement that executes commands based on the result of a condition
- Format) IF (condition) THEN command; command; ... END IF;
- Parentheses for writing conditions () can be omitted

- Search for information of the employee with employee number 7788 in the EMP table, 
- and output the employee number, employee name, and department name to the server console in a PL/SQL procedure
- Output the department name using the department number - 10: ACCOUNTING, 20: RESEARCH, 30: SALES, 40: OPERATION
```sql
DECLARE
    VEMP EMP%ROWTYPE; /* Declare a record variable to store one row (employee information) of the EMP table */
    VDNAME VARCHAR2(20) := NULL; /* Declare a scalar variable to store the department name */
BEGIN
    SELECT * INTO VEMP FROM EMP WHERE EMPNO=7788;
    
    /* Store the department name in a variable by comparing the field value storing the department number of the record variable */
    IF(VEMP.DEPTNO = 10) THEN VDNAME := 'ACCOUNTING';
    ELSIF(VEMP.DEPTNO = 20) THEN VDNAME := 'RESEARCH';
    ELSIF(VEMP.DEPTNO = 30) THEN VDNAME := 'SALES';
    ELSIF(VEMP.DEPTNO = 40) THEN VDNAME := 'OPERATION'; END IF;

    DBMS_OUTPUT.PUT_LINE('Employee Number = '||VEMP.EMPNO);
    DBMS_OUTPUT.PUT_LINE('Employee Name = '||VEMP.ENAME);
    DBMS_OUTPUT.PUT_LINE('Department Name = '||VDNAME);   
END;
/
```

- Format) IF (condition) THEN command; command; ... ELSE command; command; ... END IF;

- Search for information of the employee with employee number 7788 in the EMP table, 
- and output the employee number, employee name, and annual salary to the server console in a PL/SQL procedure
- Annual salary: If there is a commission - (salary + commission) * 12, If there is no commission - salary * 12
```sql
DECLARE
    VEMP EMP%ROWTYPE; /* Declare a record variable to store one row (employee information) of the EMP table */
    ANNUAL NUMBER(7,2) := 0; /* Declare a scalar variable to store the annual salary */    
BEGIN
    SELECT * INTO VEMP FROM EMP WHERE EMPNO=7788;
    
    IF VEMP.COMM IS NOT NULL THEN
        ANNUAL := ( VEMP.SAL + VEMP.COMM ) * 12;
    ELSE 
        ANNUAL := VEMP.SAL * 12;
    END IF;     
 
    DBMS_OUTPUT.PUT_LINE('Employee Number = '||VEMP.EMPNO);
    DBMS_OUTPUT.PUT_LINE('Employee Name = '||VEMP.ENAME);
    DBMS_OUTPUT.PUT_LINE('Employee Annual Salary = '||ANNUAL);     
END;
/
```

- Format) IF (condition) THEN command; command; ... ELSIF (condition) command; command; ... ELSE command; command; ... END IF;

- Search for information of the employee with employee number 7788 in the EMP table, 
- and output the employee number, employee name, and department name to the server console in a PL/SQL procedure
- Output the department name using the department number - 10: ACCOUNTING, 20: RESEARCH, 30: SALES, 40: OPERATION
- Also, create a PL/SQL procedure to output the names of all departments
```sql
DECLARE
    VEMP EMP%ROWTYPE; /* Declare a record variable to store one row (employee information) of the EMP table */
    VDNAME VARCHAR2(20) := NULL; /* Declare a scalar variable to store the department name */
    CURSOR VDEPT IS SELECT * FROM DEPT; /* Create a cursor to retrieve information of all departments */
BEGIN
    SELECT * INTO VEMP FROM EMP WHERE EMPNO=7788;
    
    IF(VEMP.DEPTNO = 10) THEN VDNAME := 'ACCOUNTING';
    ELSIF(VEMP.DEPTNO = 20) THEN VDNAME := 'RESEARCH';
    ELSIF(VEMP.DEPTNO = 30) THEN VDNAME := 'SALES';
    ELSIF(VEMP.DEPTNO = 40) THEN VDNAME := 'OPERATION'; END IF;

    DBMS_OUTPUT.PUT_LINE('Employee Number = '||VEMP.EMPNO);
    DBMS_OUTPUT.PUT_LINE('Employee Name = '||VEMP.ENAME);
    DBMS_OUTPUT.PUT_LINE('Department Name = '||VDNAME);   
END;
/
```
---
#### Case

- CASE: A selection statement that chooses and executes commands based on the value stored in a variable or using conditional expressions.
- Format) CASE variable WHEN comparison_value1 THEN command; command; ... WHEN comparison_value2 THEN command; command; ... END CASE
- Commands are chosen and executed based on the value stored in the variable compared with comparison values.

- Write a PL/SQL procedure to search for employee information with the employee number 7788 in the EMP table,
- and output the employee number, employee name, job, salary, and actual salary by job to the server console.
- Actual salary by job: The actual salary to be paid to employees, categorized by job.
- ANALYST: salary * 1.1, CLERK: salary * 1.2, MANAGER: salary * 1.3, PRESIDENT: salary * 1.4, SALESMAN: salary * 1.5
```
DECLARE
    VEMP EMP%ROWTYPE;
    VPAY NUMBER(7,2); -  Scalar variable to store the actual salary by job
BEGIN
    SELECT * INTO VEMP FROM EMP WHERE EMPNO=7788;
    
    CASE VEMP.JOB 
        WHEN 'ANALYST' THEN VPAY := VEMP.SAL  * 1.1;
        WHEN 'CLERK' THEN VPAY := VEMP.SAL  * 1.2;
        WHEN 'MANAGER' THEN VPAY := VEMP.SAL  * 1.3;
        WHEN 'PRESIDENT' THEN VPAY := VEMP.SAL  * 1.4;
        WHEN 'SALESMAN' THEN VPAY := VEMP.SAL  * 1.5;
    END CASE;
    
    DBMS_OUTPUT.PUT_LINE('Employee Number = '||VEMP.EMPNO);
    DBMS_OUTPUT.PUT_LINE('Employee Name = '||VEMP.ENAME);
    DBMS_OUTPUT.PUT_LINE('Employee Job = '||VEMP.JOB);
    DBMS_OUTPUT.PUT_LINE('Employee Salary = '||VEMP.SAL);        
    DBMS_OUTPUT.PUT_LINE('Actual Salary by Job = '||VPAY);      
END;
/
```
- Format) CASE WHEN condition1 THEN command; command; ... WHEN condition2 THEN command; command; ... END CASE
- Commands are chosen and executed based on the truth value of the conditions.

- Write a PL/SQL procedure to search for employee information with the employee number 7788 in the EMP table,
- and output the employee number, employee name, job, salary, and salary grade to the server console.
- Salary Grade - A: 4001~5000, B: 3001~4000, C: 2001~3000, D: 1001~2000, E: 1~1000 
```
DECLARE
    VEMP EMP%ROWTYPE;
    VGRADE VARCHAR2(1); -  Scalar variable to store the salary grade
BEGIN
    SELECT * INTO VEMP FROM EMP WHERE EMPNO=7788;
    
    CASE 
        WHEN VEMP.SAL BETWEEN 4001 AND 5000 THEN VGRADE := 'A';
        WHEN VEMP.SAL BETWEEN 3001 AND 4000 THEN VGRADE := 'B';
        WHEN VEMP.SAL BETWEEN 2001 AND 3000 THEN VGRADE := 'C';
        WHEN VEMP.SAL BETWEEN 1001 AND 2000 THEN VGRADE := 'D';
        WHEN VEMP.SAL BETWEEN 1 AND 1000 THEN VGRADE := 'E';
    END CASE;
    
    DBMS_OUTPUT.PUT_LINE('Employee Number = '||VEMP.EMPNO);
    DBMS_OUTPUT.PUT_LINE('Employee Name = '||VEMP.ENAME);
    DBMS_OUTPUT.PUT_LINE('Employee Job = '||VEMP.JOB);
    DBMS_OUTPUT.PUT_LINE('Employee Salary = '||VEMP.SAL);        
    DBMS_OUTPUT.PUT_LINE('Salary Grade = '||VGRADE);      
END;
/
```

---
#### Loop [Exit]

- LOOP: A statement to execute commands repeatedly.

- BASIC LOOP: Infinite loop - Use the EXIT command with a selection statement to exit the loop based on a condition.
- Format) LOOP command; command; ... END LOOP;

- Write a PL/SQL procedure to print numbers from 1 to 5 on the server console.
```
DECLARE
    I NUMBER(1) := 1;
BEGIN
    LOOP
        DBMS_OUTPUT.PUT_LINE(I);
        I := I + 1;
        IF I > 5 THEN EXIT; END IF;
    END LOOP;
END;
/
```

---
#### ForLoop

- FOR LOOP: Used when the number of iterations is known.
- Format) FOR INDEX_COUNTER IN [REVERSE] LOWER_BOUND..HIGH_BOUND LOOP command; command; ... END LOOP;

- Write a PL/SQL procedure to calculate the sum of numbers from 1 to 10 and print it on the server console.
```
DECLARE
    TOT NUMBER(2) := 0;
BEGIN
    FOR I IN 1..10 LOOP
        TOT := TOT + I;
    END LOOP;
    DBMS_OUTPUT.PUT_LINE('Sum of numbers from 1 to 10 = '||TOT);
END;
/
```

- FOR LOOP can be used to process multiple rows retrieved from a table.
- Format) FOR RECORD_VARIABLE IN (SELECT column1, column2, ... FROM table_name [WHERE condition]) LOOP command; command; ... END LOOP;

- If you want to retrieve multiple rows stored in the table, you can use the FOR LOOP to process them in bulk
- Format) FOR RECORD_VARIABLE IN (SELECT target, target, ... FROM table_name [WHERE condition]) LOOP command; command; ... END LOOP;

- Write a PL/SQL procedure to retrieve all rows stored in the EMP table and output the employee number and employee name to the server console
```
BEGIN
    /* Loop to retrieve all rows stored in the EMP table and store the retrieved row sequentially in the VEMP record variable */
    /* The record variable created in the FOR LOOP is automatically destroyed when the loop ends */
    /* Store all retrieved rows in the internal cursor and use the internal cursor to sequentially receive the retrieved rows and store them in the record variable for bulk processing */
    FOR VEMP IN (SELECT * FROM EMP) LOOP
        DBMS_OUTPUT.PUT_LINE('Employee Number = '||VEMP.EMPNO||', Employee Name = '||VEMP.ENAME);
    END LOOP;
END;
/
```
---
## Cursor
---

- Cursor (CURSOR): Provides a function to store retrieved rows of a table and sequentially provide the retrieved rows
- 1.Implicit Cursor: Automatically created to store the result of a single row query
- 2.Explicit Cursor: Manually created to store the result of a multi-row query

- How to create and use an explicit cursor
- Cursors are created in the declaration section and use the OPEN, FETCH, CLOSE commands in the execution section to receive and use the retrieved rows stored in the cursor
- Format) CURSOR cursor_name IS SELECT target, target, ... FROM table_name [WHERE condition]
- Write the SELECT command to ensure that the created cursor stores the retrieved rows

- Open Cursor: Move the cursor position to receive the first retrieved row from the stored retrieved rows - executed once
- Format) OPEN cursor_name
- Use Cursor: Receive the retrieved row at the cursor position and store the column values of the retrieved row in variables (the cursor automatically moves to the next row) - repeated execution
- Format) FETCH cursor_name INTO variable_name, variable_name, ...
- Close Cursor: End cursor usage - executed once
- Format) CLOSE cursor_name

- Write a PL/SQL procedure to retrieve all rows stored in the EMP table and output the employee number and employee name to the server console
```
DECLARE
    CURSOR C IS SELECT * FROM EMP;
    VEMP EMP%ROWTYPE; 
BEGIN    
    OPEN C;
    
    LOOP
        FETCH C INTO VEMP; /* Receive the retrieved row at the cursor position and store it in the record variable */
        EXIT WHEN C%NOTFOUND; /* End loop when no retrieved row exists at the cursor position */
        DBMS_OUTPUT.PUT_LINE('Employee Number = '||VEMP.EMPNO||', Employee Name = '||VEMP.ENAME);
    END LOOP;    
    
    CLOSE C;
END;
/
```
---
#### WhileLoop
- WHILE LOOP: A loop used when the number of repetitions is uncertain
- Format) WHILE condition LOOP command; command; ... END LOOP
- Repeats the commands if the condition is true, ends the loop if the condition is false

- Write a PL/SQL procedure to calculate the sum of numbers in the range 1 to 10 and output it to the server console
```
DECLARE
    I NUMBER(2) := 1;
    TOT NUMBER(2) := 0;
BEGIN
    WHILE I <= 10 LOOP
        TOT := TOT + I;
        I := I + 1;
    END LOOP;    
    DBMS_OUTPUT.PUT_LINE('Sum of numbers in the range 1 to 10 = '||TOT);
END;
/
```
---
#### StoredProcedure

- Stored Procedure: Assign a name to a PL/SQL procedure and store it, then call and use it when needed

- Create Stored Procedure
- Format) CREATE [OR REPLACE] PROCEDURE stored_procedure_name[(parameter [MODE] datatype, parameter [MODE] datatype, ...)]
-       IS [variable_declaration_section] BEGIN command; command; ... END
- Modes for stored procedure parameters
- 1.IN: Used to write parameters that receive values when the stored procedure is called and store them for use in the PL/SQL procedure
- 2.OUT: Used to write parameters that receive binding variables when the stored procedure is called and store the execution results of the PL/SQL procedure to provide them externally
- 3.INOUT: Used to write parameters that provide both IN and OUT mode functions

- Retrieve all rows (department information) stored in the DEPT_COPY table
```
SELECT * FROM DEPT_COPY;
```

- Create a stored procedure to delete all rows (department information) stored in the DEPT_COPY table
```
CREATE OR REPLACE PROCEDURE DELETE_ALL_DEPT_COPY IS 
BEGIN
    DELETE FROM DEPT_COPY;
    COMMIT;
END;
/
```
- USER_SOURCE: A dictionary that provides information related to stored procedures and stored functions
```
SELECT NAME,TEXT FROM USER_SOURCE WHERE NAME='DELETE_ALL_DEPT_COPY';
```

- Stored Procedure - Execute the commands written in the PL/SQL procedure of the stored procedure
- Format) EXECUTE stored_procedure_name[({binding_variable_name|value}, {binding_variable_name|value}, ...)]

- Call DELETE_ALL_DEPT_COPY stored procedure
```
EXECUTE DELETE_ALL_DEPT_COPY;
```

- Retrieve all rows (department information) stored in the DEPT_COPY table
```
SELECT * FROM DEPT_COPY;
```

- Create a stored procedure that receives an employee number as a parameter, retrieves the employee information of the received employee number from the EMP table, and stores the employee name in a parameter to provide it externally
```
CREATE OR REPLACE PROCEDURE SELECT_EMP(VEMPNO IN EMP.EMPNO%TYPE, VENAME OUT EMP.ENAME%TYPE) IS
BEGIN
    SELECT ENAME INTO VENAME FROM EMP WHERE EMPNO=VEMPNO;
END;
/
```

- Command to check error messages
```
SHOW ERROR;
```

- Binding variables are needed to use the result values generated by the PL/SQL procedure commands in the OUT MODE parameters
- Binding Variable: A system variable that can only be used in the current user's session
- Format) VARIABLE binding_variable_name datatype
```
VARIABLE VAR_NAME VARCHAR2(20);
```

- Call SELECT_EMP stored procedure - pass values to IN MODE parameters and pass binding variables to OUT MODE parameters
- Use the [:] symbol before the variable to store the result value generated by the PL/SQL procedure commands in the OUT MODE parameter
```
EXECUTE SELECT_EMP(7788,:VAR_NAME);
```

- How to output the values stored in the binding variable
- Format) PRINT binding_variable_name
```
PRINT VAR_NAME; 
```
- Delete Stored Procedure
- Format) DROP PROCEDURE stored_procedure_name

- Delete DELETE_ALL_DEPT_COPY stored procedure
```
SELECT NAME,TEXT FROM USER_SOURCE WHERE NAME='DELETE_ALL_DEPT_COPY';
DROP PROCEDURE DELETE_ALL_DEPT_COPY;
SELECT NAME,TEXT FROM USER_SOURCE WHERE NAME='DELETE_ALL_DEPT_COPY';
```
---
#### Store Function
- Stored Function: Provides similar functions as a stored procedure, but a stored function must return a result value

- Create Stored Function
- Format) CREATE [OR REPLACE] FUNCTION stored_function_name[(parameter [MODE] datatype, parameter [MODE] datatype, ...)]
-       RETURN datatype IS [variable_declaration_section] BEGIN command; command; ... RETURN return_value; END

- Create a stored function that receives an employee number as a parameter, retrieves the employee information of the received employee number from the EMP table, and returns the result of multiplying the salary by 2
```
CREATE OR REPLACE FUNCTION CAL_SAL(VEMPNO IN EMP.EMPNO%TYPE) RETURN NUMBER IS 
    VSAL EMP.SAL%TYPE; /* Reference variable to store the salary of the retrieved employee */
BEGIN
    SELECT SAL INTO VSAL FROM EMP WHERE EMPNO=VEMPNO;
    RETURN ( VSAL * 2 );
END;
/
```

- The USER_SOURCE dictionary can be used to check the stored function
```
SELECT NAME,TEXT FROM USER_SOURCE WHERE NAME='CAL_SAL';
```

- Declare binding variables to store the return value of the stored function
```
VARIABLE VAR_SAL NUMBER;
```

- Call the stored function and store the return value in the binding variable
```
EXECUTE :VAR_SAL := CAL_SAL(7788);
```

- Output the value stored in the binding variable
```
PRINT VAR_SAL;
```

- Stored functions can be included in SQL commands and used like built-in Oracle functions
```
SELECT EMPNO,ENAME,SAL,CAL_SAL(EMPNO) AS Special_Allowance FROM EMP;
```

- Delete Stored Function
- Format) DROP FUNCTION stored_function_name

- Delete DELETE_ALL_DEPT_COPY stored procedure
```
SELECT NAME,TEXT FROM USER_SOURCE WHERE NAME='CAL_SAL';
DROP FUNCTION CAL_SAL;
SELECT NAME,TEXT FROM USER_SOURCE WHERE NAME='CAL_SAL';
```
---
#### Trigger

- Trigger: Automatically execute the commands of a PL/SQL procedure when a DML command is executed on a specific table

- Create Trigger
- Format) CREATE [OR REPLACE] TRIGGER trigger_name {AFTER|BEFORE} {INSERT|UPDATE|DELETE} ON table_name
-       [FOR EACH ROW] [WITH condition] BEGIN command; command; ... END
- FOR EACH ROW: An option to create a row-level trigger, if omitted, a statement-level trigger is created
- Statement-level Trigger: Executes the commands written in the PL/SQL procedure once when an event occurs by executing a DML command
- Row-level Trigger: Executes the commands written in the PL/SQL procedure as many times as the number of rows where the event occurred when a DML command is executed
- COMMIT or ROLLBACK commands cannot be used in the PL/SQL procedure commands written in the trigger

- Create EMP_COPY table by retrieving all rows and columns stored in the EMP table and inserting the retrieved rows
```
CREATE TABLE EMP_COPY AS SELECT * FROM EMP;
```

- Check attributes and stored rows of EMP_COPY table


```
DESC EMP_COPY;
SELECT * FROM EMP_COPY;
```

- Create a trigger to print a message on the server console when a row (employee information) is inserted into the EMP_COPY table
```
CREATE OR REPLACE TRIGGER EMP_COPY_TRIGGER AFTER INSERT ON EMP_COPY
BEGIN
    DBMS_OUTPUT.PUT_LINE('A new employee has joined.');
END;
/
```

- USER_TRIGGERS: Dictionary to provide trigger information
```
SELECT TRIGGER_NAME, TRIGGER_TYPE, TRIGGERING_EVENT, TABLE_NAME FROM USER_TRIGGERS;
```
- Insert and save a row in the EMP_COPY table - Automatically execute the PL/SQL procedure command written in the trigger
```
INSERT INTO EMP_COPY VALUES(9002, 'Jeon Woochi', 'CLERK', 9000, SYSDATE, 1500, NULL, 40);
SELECT * FROM EMP_COPY WHERE EMPNO=9002;
COMMIT;
```

- Delete the trigger
- Syntax: DROP TRIGGER trigger_name

- Delete the EMP_COPY_TRIGGER trigger
```
DROP TRIGGER EMP_COPY_TRIGGER;
SELECT TRIGGER_NAME, TRIGGER_TYPE, TRIGGERING_EVENT, TABLE_NAME FROM USER_TRIGGERS;
```
- Create the EMP_HISTORY table - Attributes: SERIAL (NUMBER - PRIMARY KEY), EMPNO (NUMBER), REASON (VARCHAR2)
```
CREATE TABLE EMP_HISTORY(SERIAL NUMBER PRIMARY KEY, EMPNO NUMBER(4), REASON VARCHAR2(50));
```

- Create the EMP_HISTORY_SEQ sequence - Sequence to provide automatically incremented values stored in the SERIAL column of the EMP_HISTORY table
```
CREATE SEQUENCE EMP_HISTORY_SEQ;
```

- Create a trigger that inserts a row into the EMP_HISTORY table with the reason for insertion, update, or deletion 
- when a row is inserted, updated, or deleted in the EMP_COPY table
```
CREATE OR REPLACE TRIGGER EMP_COPY_TRIGGER AFTER INSERT OR UPDATE OR DELETE ON EMP_COPY FOR EACH ROW
BEGIN
    /* :NEW.column_name: Represents the new column value of the table where the event occurred - Used in INSERT or UPDATE commands */
    /* :OLD.column_name: Represents the existing column value of the table where the event occurred - Used in UPDATE or DELETE commands */
    IF INSERTING THEN /* When an INSERT command is executed */
        INSERT INTO EMP_HISTORY VALUES(EMP_HISTORY_SEQ.NEXTVAL, :NEW.EMPNO, 'Join');    
    ELSIF UPDATING THEN /* When an UPDATE command is executed */
        IF :NEW.DEPTNO <> :OLD.DEPTNO THEN /* When the department number changes */
            INSERT INTO EMP_HISTORY VALUES(EMP_HISTORY_SEQ.NEXTVAL, :OLD.EMPNO, 'Department Transfer');    
        ELSIF :NEW.SAL <> :OLD.SAL THEN  /* When the salary changes */
            INSERT INTO EMP_HISTORY VALUES(EMP_HISTORY_SEQ.NEXTVAL, :OLD.EMPNO, 'Salary Change');    
        ELSE     
            INSERT INTO EMP_HISTORY VALUES(EMP_HISTORY_SEQ.NEXTVAL, :OLD.EMPNO, 'Personal Reason');    
        END IF;
    ELSIF DELETING THEN /* When a DELETE command is executed */
        INSERT INTO EMP_HISTORY VALUES(EMP_HISTORY_SEQ.NEXTVAL, :OLD.EMPNO, 'Resignation');    
    END IF;
END;
/
```
- Check the trigger
```
SELECT TRIGGER_NAME, TRIGGER_TYPE, TRIGGERING_EVENT, TABLE_NAME FROM USER_TRIGGERS;
```

- Insert and save a row in the EMP_COPY table - Execute the PL/SQL procedure command of the EMP_COPY_TRIGGER trigger
```
INSERT INTO EMP_COPY VALUES(9003, 'Iljimae', 'CLERK', 9000, SYSDATE, 1500, NULL, 40);
SELECT * FROM EMP_COPY WHERE EMPNO=9003;
SELECT * FROM EMP_HISTORY;
```

- Change the column values of the row saved in the EMP_COPY table - Execute the PL/SQL procedure command of the EMP_COPY_TRIGGER trigger
```
UPDATE EMP_COPY SET DEPTNO=30 WHERE EMPNO=9003;
SELECT * FROM EMP_COPY WHERE EMPNO=9003;
SELECT * FROM EMP_HISTORY;

UPDATE EMP_COPY SET SAL=2000 WHERE EMPNO=9003;
SELECT * FROM EMP_COPY WHERE EMPNO=9003;
SELECT * FROM EMP_HISTORY;

UPDATE EMP_COPY SET MGR=9001 WHERE EMPNO=9003;
SELECT * FROM EMP_COPY WHERE EMPNO=9003;
SELECT * FROM EMP_HISTORY;
```

- Delete the row saved in the EMP_COPY table - Execute the PL/SQL procedure command of the EMP_COPY_TRIGGER trigger
```
DELETE FROM EMP_COPY;
SELECT * FROM EMP_COPY;
SELECT * FROM EMP_HISTORY;
COMMIT;
```